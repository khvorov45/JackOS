/** Handles graphics output to the screen */

class Screen {

    static int screen;
    static bool black;
    static Array drawpx, erasepx;

    function void init() {
        
        var int i;

        let screen = 16384;
        let black = true;
        let drawpx = Array.new(16);
        let erasepx = Array.new(16);

        let drawpx[0] = 1;
        let drawpx[15] = ~32767;
        let i = 1;
        while (i < 15) {
            let drawpx[i] = drawpx[i - 1] + drawpx[i - 1];
            let i = i + 1;
        }

        let i = 0;
        while (i < 16) {
            let erasepx[i] = ~drawpx[i];
            let i = i + 1;
        }

        return;
    }

    /** Erases the entire screen */
    function void clearScreen() {
        var int i;
        let i = 0;
        while (i < 8192) {
            let screen[i] = 0;
            let i = i + 1;
        }
        return;
    }

    /** Sets a color (white = false, black = true) to be used for all further
    draw* commands */
    function void setColor(boolean b) {
        let black = b;
        return;
    }

    /** Draws the (x, y) pixel */
    function void drawPixel(int x, int y) {
        var int inrow, reg, inreg;
        let inrow = x / 16;
        let reg = (y * 32) + inrow;
        let inreg = x - (inrow * 16);
        let screen[reg] = screen[reg] & erasepx[inreg];
        if (black) {
            let screen[reg] = screen[reg] + drawpx[inreg];
        }
        return;
    }

    /** Draws a line from pixel (x1, y1) to pixel (x2, y2) */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int addx, addy, diffx, diffy, adymbdx;
        if (x2 = x1) {
            if (y1 = y2) {
                do Screen.drawPixel(x1, y1);
                return;
            }
            do Screen.drawVline(x1, y1, y2);
            return;
        }
        if (y2 = y1) {
            do Screen.drawHline(y1, x1, x2);
            return;
        }
        let addx = 0;
        let addy = 0;
        let diffx = x2 - x1;
        let diffy = y2 - y1;
        let adymbdx = 0;
        while ((Math.abs(addx - diffx) > 0) & (Math.abs(addy - diffy) > 0)) {
            do Screen.drawPixel(x1 + addx, y1 + addy);
            if (adymbdx < 0) {
                if (diffx > 0) {
                    let addx = addx + 1;
                }
                else {
                    let addx = addx - 1;
                }
                let adymbdx = adymbdx + Math.abs(diffy);
            }
            else {
                if (diffy > 0) {
                    let addy = addy + 1;
                }
                else {
                    let addy = addy - 1;
                }
                let adymbdx = adymbdx - Math.abs(diffx);
            }
        }
        do Screen.drawPixel(x2, y2);
        return;
    }

    /* Draws a horizontal line from x1 to x2 at y */
    function void drawHline(int y, int x1, int x2) {
        var int start, end, diff, i;
        if (x1 > x2) {
            let start = x2;
            let end = x1;
        }
        else {
            let start = x1;
            let end = x2;
        }
        let diff = end - start;
        let i = 0;
        while (i < diff) {
            do Screen.drawPixel(start + i, y);
            let i = i + 1;
        }
        do Screen.drawPixel(end, y);
        return;
    }

    /* Draws a vertical line from y1 to y2 at x */
    function void drawVline(int x, int y1, int y2) {
        var int start, end, diff, i;
        if (y1 > y2) {
            let start = y2;
            let end = y1;
        }
        else {
            let start = y1;
            let end = y2;
        }
        let diff = end - start;
        let i = 0;
        while (i < diff) {
            do Screen.drawPixel(x, start + i);
            let i = i + 1;
        }
        do Screen.drawPixel(x, end);
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1) and whose
    bottom right corner is (x2, y2) */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int start, end, diff, i;
        if (y1 > y2) {
            let start = y2;
            let end = y1;
        }
        else {
            let start = y1;
            let end = y2;
        }
        let diff = end - start;
        let i = 0;
        while (i < diff) {
            do Screen.drawHline(start + i, x1, x2);
            let i = i + 1;
        }
        do Screen.drawHline(end, x1, x2);
        return;
    }

    /** Draws a filled circle of radius r <= 181 around (x, y) */
    function void drawCircle(int x, int y, int r) {
        var int r2, i, xdiff;
        let r2 = r * r;
        do Screen.drawPixel(x - r, y);
        let i = -r + 1;
        while (i < r) {
            let xdiff = Math.sqrt(r2 - (i * i));
            do Screen.drawHline(y + i, x - xdiff, x + xdiff);
            let i = i + 1;
        }
        do Screen.drawPixel(x + r, y);
        return;
    }
}